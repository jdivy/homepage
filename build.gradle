repositories {
    mavenCentral()
}

configurations {
    sshAntTask
}

dependencies {
    sshAntTask "org.apache.ant:ant-jsch:1.9.3"
    sshAntTask "jsch:jsch:0.1.29"
}

task update {
    def user = System.getenv("USER")
    def home = System.getenv("HOME")
    description = "Update files on remote server as $user."

    // Redefine scp Ant task, with the classpath property set to our newly defined
    // sshAntTask configuration classpath.
    ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
            classpath: configurations.sshAntTask.asPath)

    doLast {
        def console = System.console()
        def passphrase = console.readPassword("%s: ", "Please enter passphrase for the key file ($home/.ssh/id_rsa)")

        // Invoke the scp Ant task. (Use gradle -i update to see the output of the Ant task.)
        ant.scp(todir: "${user}@jackivy.com:/tmp",
                keyfile: "${home}/.ssh/id_rsa",
                passphrase: passphrase as String, // Use phassphrase entered by the user.
                verbose: 'true') {
            fileset(dir: 'web') {
                include(name: '**/**')
            }
        }
    }
}
